buildscript {
	ext {
		springBootVersion = '2.0.0.M3'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'de.undercouch:gradle-download-task:3.2.0'
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
apply plugin: 'de.undercouch.download'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
  compile('org.projectlombok:lombok:1.16.14')
  compile('org.tensorflow:tensorflow:1.3.0-rc1')
  compile('com.google.protobuf:protobuf-java:3.3.1')
  compile('commons-io:commons-io:2.5')

	testCompile('org.springframework.restdocs:spring-restdocs-core:1.2.0.RELEASE')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.2.0.RELEASE')
	testCompile('org.springframework.restdocs:spring-restdocs-asciidoctor:1.2.0.RELEASE')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

bootRun {
  //http://stackoverflow.com/a/31328621/4985623
  systemProperties System.properties
}

mainClassName = 'com.newsplore.Application'

import de.undercouch.gradle.tasks.download.Download

task downloadInceptionFrozen(type: Download) {
  onlyIfNewer true
  src 'https://storage.googleapis.com/download.tensorflow.org/models/inception_v3_2016_08_28_frozen.pb.tar.gz'
  dest new File(buildDir, 'inception_v3_2016_08_28_frozen.pb.tar.gz')
}

task fetchInceptionFrozenModel(dependsOn: downloadInceptionFrozen, type: Copy) {
  from tarTree(downloadInceptionFrozen.dest)
  into 'src/main/resources/inception-v3'
}

build.dependsOn fetchInceptionFrozenModel